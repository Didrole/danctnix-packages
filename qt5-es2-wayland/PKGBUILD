# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-es2-wayland
_pkgname=qt5-wayland
_qtver=5.14.1
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64' 'aarch64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides APIs for Wayland - with OpenGL ES 2.0 support'
depends=('qt5-es2-declarative' 'libxcomposite')
makedepends=('vulkan-headers')
provides=("qt5-wayland=$pkgver")
conflicts=('qt5-wayland')
groups=('qt' 'qt5')
_pkgfqn="${_pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('2a03b9f554e88c5824ef237c814b3dd45844c022e97be0e091f4a502ca4c9520')

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build

  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${_pkgname}
}
 
