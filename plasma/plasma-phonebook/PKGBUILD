# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-phonebook
pkgver=24.02.0
pkgrel=1
pkgdesc="Phonebook application for Mobile Devices running Plasma"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=(
  kconfig
  kcontacts
  kcoreaddons
  kirigami
  kirigami-addons
  kpeople
  qt6-declarative
  qt6-svg
)
makedepends=(
  cmake
  extra-cmake-modules
)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('94cc358c1b7adcdabf6d1b17968c8699211e172e13059b37485f6084651b5fb8')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
