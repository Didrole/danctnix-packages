# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <tech@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-dialer
pkgver=22.02
pkgrel=2
pkgdesc="Phone Dialer for Plasma Mobile"
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('desktop-file-utils' 'kcontacts' 'modemmanager-qt' 'kpeople' 'telepathy-qt' 'libphonenumber'
         'kdbusaddons' 'knotifications' 'telepathy-mission-control' 'pulseaudio-qt' 'qt5-feedback'
         'qt5-quickcontrols2' 'qtmpris' 'callaudiod')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools')
source=("https://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz"
        'port-to-callaudiod.patch')
install=$pkgname.install
md5sums=('32421c5395fd195ec886861fad923870'
         '17da259328b8dcc1f67535d7ad4b5e78')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../port-to-callaudiod.patch
  cd ..

  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
