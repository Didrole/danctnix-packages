# Maintainer: Danct12 <danct12@disroot.org>
pkgname=hfd-service
pkgver=0.2.2
pkgrel=1
pkgdesc="Service that manages human feedback devices on mobile devices"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.com/ubports/core/hfd-service"
license=('LGPL-3.0-only')
depends=('accountsservice' 'lomiri-deviceinfo' 'qt5-base' 'qt5-feedback')
makedepends=('cmake' 'cmake-extras')
source=(https://gitlab.com/ubports/core/hfd-service/-/archive/$pkgver/hfd-service-$pkgver.tar.gz
        plugin_install_dir.patch)

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 -N < ../plugin_install_dir.patch
}

build() {
    cmake -B build -S "$pkgname-$pkgver" \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DENABLE_LIBHYBRIS=OFF \
        -DENABLE_LIBGBINDER=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}

md5sums=('c1629aff85b8f814d6638cd1560928d1'
         'f65d8618960aa3466db99708abd974de')
