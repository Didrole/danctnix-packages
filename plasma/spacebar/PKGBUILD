# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=spacebar
pkgver=0.1
pkgrel=1
pkgdesc="SMS and messaging application for Mobile Devices running Plasma"
arch=('x86_64' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('kcontacts'
    'knotifications'
    'kirigami2'
    'kpeople'
    'ki18n'
    'qt5-quickcontrols2'
    'telepathy-qt')
makedepends=('cmake'
    'extra-cmake-modules'
    'git')
source=("https://invent.kde.org/plasma-mobile/spacebar/-/archive/v$pkgver/spacebar-v$pkgver.tar.gz")
md5sums=('0f58ea6767303f66d79d4d21944188f0')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../spacebar-v$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
