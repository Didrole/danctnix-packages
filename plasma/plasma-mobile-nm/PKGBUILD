# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-mobile-nm
_pkgname=plasma-nm
pkgver=5.22.5
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections - Mobile Edition'
arch=(x86_64 armv7h aarch64)
url='https://plasma-mobile.org/'
license=(GPL2)
depends=(plasma-workspace networkmanager-qt qca mobile-broadband-provider-info)
makedepends=(extra-cmake-modules openconnect)
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://download.kde.org/stable/plasma/$pkgver/$_pkgname-$pkgver.tar.xz")
sha256sums=('7fab9fa175a7b0caf21ff19dddda9d1ea4d05cd3962fe1a2c726404203b9fc49')

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_MOBILE=True
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
