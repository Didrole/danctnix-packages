# Maintainer: Danct12 <danct12@disroot.org>

pkgname=lomiri-deviceinfo
pkgver=0.2.2
pkgrel=1
pkgdesc='Library to detect and configure devices'
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.com/ubports/development/core/deviceinfo"
license=('GPL-3.0-only')
depends=('yaml-cpp')
makedepends=('cmake' 'cmake-extras' 'git')
checkdepends=('gcovr' 'gtest')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/ubports/development/core/deviceinfo/-/archive/$pkgver/deviceinfo-$pkgver.tar.gz")

build() {
  cmake -B build -S "deviceinfo-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCONFIG_PATH=/etc/lomiri-deviceinfo
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
sha256sums=('c964cde6ce618a42829be05c48e70edb464f61d604f1e89f4c685d981e8d8aa9')
