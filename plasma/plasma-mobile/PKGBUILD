# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mobile
pkgver=6.0.2
pkgrel=1
pkgdesc="Plasma shell for mobile devices"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  bluez-qt
  libplasma
  kirigami-addons
  milou
  plasma-nano
  plasma-nm
  plasma-pa
  plasma-workspace
)
makedepends=(
  cmake
  extra-cmake-modules
)
optdepends=('hfd-service: Haptic feedback')
replaces=('plasma-phone-components')
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
sha512sums=('9247012cb63dd87c8ad08c345447068c9d0404ba67487536223143d625ec724ab5bd3fac3d8ec20aac3550ccb8ead8948733cdc94681fc61255671f4706bfd5d')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
