# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mobile
pkgver=6.1.2
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc="Plasma shell for mobile devices"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  bluez-qt
  libplasma
  kirigami-addons
  milou
  plasma-nano
  plasma-nm
  plasma-pa
  plasma-workspace=$pkgver
)
makedepends=(
  cmake
  extra-cmake-modules
)
optdepends=('hfd-service: Haptic feedback')
replaces=('plasma-phone-components')
source=("https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
sha512sums=('db59764c89c91245fca60569a996a61e64e7e7eb0a869e10f65fda47dd6c9a7bb1843dfd7bc24d4d6c4cfb2a55a9d5f0a8f3e3ade6f8000edd8509a3efa5479e')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
