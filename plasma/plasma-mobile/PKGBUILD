# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mobile
pkgver=5.24.0
pkgrel=1
pkgdesc="General UI components for Plasma Phone including shell, containment and applets."
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('desktop-file-utils' 'plasma-workspace' 'modemmanager-qt' 'kpeople' 'telepathy-qt' 'libphonenumber'
         'plasma-wayland-session' 'pipewire-media-session')
makedepends=('cmake' 'baloo' 'extra-cmake-modules' 'kdoctools')
replaces=('plasma-phone-components')
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
sha512sums=('4e9d8b167cf804c1d36315aa883b095946483cc870fe12edb00f326b131a12dd5268cef30bf3c15c401e9a4aeea173c0873c66c878d1ed7e073ca588b79df0b5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
