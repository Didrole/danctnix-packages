# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mobile
pkgver=6.0.1
pkgrel=1
pkgdesc="Plasma shell for mobile devices"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  bluez-qt
  libplasma
  kirigami-addons
  milou
  plasma-nano
  plasma-nm
  plasma-pa
  plasma-workspace
)
makedepends=(
  cmake
  extra-cmake-modules
)
optdepends=('hfd-service: Haptic feedback')
replaces=('plasma-phone-components')
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
sha512sums=('71604189a2d1e542dce6651aff2a5c0de0d4dc4b244d6497bc1575b96897a7526f56103dd4a90b14abf7c8f9b3797b9cf3d3e6d7cfa981f76c75cca890483ad4')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
