# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-phone-components
pkgver=5.21.3
pkgrel=1
pkgdesc="General UI components for Plasma Phone including shell, containment and applets."
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('desktop-file-utils' 'plasma-workspace' 'ofono' 'libqofono-qt5' 'kpeople' 'telepathy-qt' 'libphonenumber' 'plasma-wayland-session')
makedepends=('git' 'cmake' 'baloo' 'extra-cmake-modules' 'kdoctools')
source=("https://invent.kde.org/plasma/plasma-phone-components/-/archive/v${pkgver}/plasma-phone-components-v${pkgver}.tar.gz")
install=$pkgname.install
sha512sums=('db805352a6f88d9636ea9e8d67de6de857474334ccbe55a9b97d46c0d3bf8ad31dd23c340c9b47f752fb37248b141e4d7812cd57445242fad2f9e113c586a5d2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-v${pkgver} \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
