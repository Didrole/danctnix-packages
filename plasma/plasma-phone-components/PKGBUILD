# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-phone-components
pkgver=5.21.0
pkgrel=1
pkgdesc="General UI components for Plasma Phone including shell, containment and applets."
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('desktop-file-utils' 'plasma-workspace' 'ofono' 'libqofono-qt5' 'kpeople' 'telepathy-qt' 'libphonenumber' 'plasma-wayland-session')
makedepends=('git' 'cmake' 'baloo' 'extra-cmake-modules' 'kdoctools')
source=("https://invent.kde.org/plasma/plasma-phone-components/-/archive/v${pkgver}/plasma-phone-components-v${pkgver}.tar.gz")
install=$pkgname.install
sha512sums=('b16aa20422ba939d928b72a47cf9b972f06ee98532c025658016d08c5e059c0380bf3f9312c24ff2bb40a27652d6d4dadcb21c0a6e31a207e0d7025f3c1fa37c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-v${pkgver} \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
