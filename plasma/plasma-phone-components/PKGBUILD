# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-phone-components
pkgver=5.23.0
pkgrel=1
pkgdesc="General UI components for Plasma Phone including shell, containment and applets."
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('desktop-file-utils' 'plasma-workspace' 'ofono' 'libqofono-qt5' 'kpeople' 'telepathy-qt' 'libphonenumber' 'plasma-wayland-session' 'pipewire-media-session')
makedepends=('git' 'cmake' 'baloo' 'extra-cmake-modules' 'kdoctools')
source=("https://invent.kde.org/plasma/plasma-phone-components/-/archive/v${pkgver}/plasma-phone-components-v${pkgver}.tar.gz"
        'revert-modem-manager.patch')
install=$pkgname.install
sha512sums=('4d6d1d79cba85352adadeb68ea93ec2d163f16e81f1d4ac184381ced5c197e39f1d63106f48c9c31a247f4e7d7e86a3f253cd0024914536d4dd91f312c625d76'
            '768ef03d8c2f216ec837cd3cbaccbe0413ed4ed53b556b9d458b6350735cccef94034a09b5d4cadaab8f8ae3bdec043fc7b0d92740e923bb49acbc20fb1e1789')

prepare() {
  cd "$pkgname-v${pkgver}"

  # Revert modem manager patch, this can be added back in when it's done
  patch -p1 < ../revert-modem-manager.patch

  cd "$srcdir"

  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-v${pkgver} \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
