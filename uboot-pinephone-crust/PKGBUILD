# U-Boot: PinePhone/PineTab
# Maintainer: Danct12 <danct12@disroot.org>

buildarch=8

pkgname=uboot-pinephone-crust
pkgver=v2020.04
pkgrel=0
pkgdesc="U-Boot for Pine64 -- CRUST EDITION"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('bc' 'python' 'swig' 'dtc')
provides=('uboot-pinephone')
conflicts=('uboot-pinephone')
install=${pkgname}.install

_commit_uboot="f0b298c504daae9da171bac950c036432cc00201"
_commit_atf="5eeb091ade2f6a51f2e34aa9e62830afdc032aa1"

source=("u-boot-$_commit_uboot.tar.gz::https://gitlab.com/pine64-org/u-boot/-/archive/${_commit_uboot}/u-boot-${_commit_uboot}.tar.gz"
        "arm-trusted-firmware-$_commit_atf.tar.gz::https://github.com/ARM-software/arm-trusted-firmware/archive/${_commit_atf}.tar.gz"
        '0001-ATF-set-fno-stack-protector.patch'
        '0001-configs-Add-Pinetab-configuration.patch'
        'boot.txt'
        'mkscr')
md5sums=('c04fe9cb85b9e5f6dc20eed79e0f6149'
         'cc84cfb04cf4174b7598c33d55939f54'
         '348a6943a1c220047ce5312b59da95b6'
         'bdb8e9686df7fccbedf8577a8d3e350a'
         '06d85506cdc2054ba76b552bf29499c2'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd arm-trusted-firmware-${_commit_atf}
  patch -N -p1 < ../0001-ATF-set-fno-stack-protector.patch

  cd ../u-boot-${_commit_uboot}
  patch -N -p1 < ../0001-configs-Add-Pinetab-configuration.patch
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd arm-trusted-firmware-${_commit_atf}

  make PLAT=sun50i_a64 DEBUG=1 bl31
  cp build/sun50i_a64/debug/bl31.bin ../u-boot-${_commit_uboot}

  cd ../u-boot-${_commit_uboot}

  # This builds the PineTab.
  echo "Building U-Boot for PineTab..."
  make distclean
  make pinetab_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinetab.bin

  # This builds the PinePhone.
  echo "Building U-Boot for PinePhone..."
  make pinephone_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinephone.bin
}

package() {
  cd u-boot-${_commit_uboot}

  mkdir -p "${pkgdir}"/boot

  cp u-boot-sunxi-with-spl-pinetab.bin "${pkgdir}"/boot
  cp u-boot-sunxi-with-spl-pinephone.bin "${pkgdir}"/boot

  cp ../boot.txt ../mkscr "${pkgdir}"/boot
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d "${pkgdir}"/boot/boot.txt "${pkgdir}"/boot/boot.scr
}
