# U-Boot: Pinephone Pro based on PKGBUILD for RK3399
# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <furkan@fkardame.com>

pkgname=uboot-pinephonepro
pkgver=2022.01
pkgrel=3
_tfaver=2.6
pkgdesc="U-Boot for Pinephone Pro"
arch=('aarch64')
url='https://git.sr.ht/~martijnbraam/u-boot'
license=('GPL')
depends=('uboot-tools') # we need this for mkscr
makedepends=('git' 'arm-none-eabi-gcc' 'dtc' 'bc' 'python3')
provides=('uboot')
conflicts=('uboot')
install=${pkgname}.install
backup=("boot/boot.txt")
source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "https://git.trustedfirmware.org/TF-A/trusted-firmware-a.git/snapshot/trusted-firmware-a-$_tfaver.tar.gz"
        'rockchip-Add-initial-support-for-the-PinePhone-Pro.patch'
        'Configure-USB-power-settings-for-PinePhone-Pro.patch'
        'mkscr'
        'boot.txt')
sha256sums=('81b4543227db228c03f8a1bf5ddbc813b0bb8f6555ce46064ef721a6fc680413'
            '4e59f02ccb042d5d18c89c849701b96e6cf4b788709564405354b5d313d173f7'
            '7c3d76f4bee0e54900142043241248072e334387065212141e1f600afe0aafba'
            'b750ba47843defafd8be1cc2615983c93e9cde5a4f5a7b55308a6f00f3fe6611'
            'a4fc8b6b92bc364d6542670d294aa618a8501fb8729f415cc0a3eed776ef0c8e'
            'e50196101e7619a85f3406f4f2408fd9f8c0fc1f928d9185dde1f0b3868390e6')

prepare() {
  cd u-boot-${pkgver/rc/-rc}
  patch -p1 -N < ../rockchip-Add-initial-support-for-the-PinePhone-Pro.patch
  patch -p1 -N < ../Configure-USB-power-settings-for-PinePhone-Pro.patch

  # Change baudrate from 1.5mbps to 115200, it makes it Tow-Boot compliance. (hi samueldr)
  # https://github.com/Tow-Boot/Tow-Boot/blob/released/support/builders/tow-boot/default.nix
  echo ':: Patching baud rate'
  (PS4=" $ "
    for f in configs/*rk3399* configs/*rk3328*; do
      (set -x
      sed -i"" -e 's/CONFIG_BAUDRATE=1500000/CONFIG_BAUDRATE=115200/' "$f"
      )
    done
    for f in arch/arm/dts/*rk3399*.dts* arch/arm/dts/*rk3328*.dts*; do
      (set -x
      sed -i"" -e 's/serial2:1500000n8/serial2:115200n8/' "$f"
      )
    done
  )
}

build() {
  cd trusted-firmware-a-$_tfaver
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  make PLAT=rk3399
  cp build/rk3399/release/bl31/bl31.elf ../u-boot-${pkgver/rc/-rc}
  cd ../u-boot-${pkgver/rc/-rc}
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  make pinephone-pro-rk3399_defconfig
  echo 'CONFIG_IDENT_STRING=" DanctNIX ARM"' >> .config
  echo 'CONFIG_BOOTDELAY=0' >> .config

  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}/boot"
  cp idbloader.img u-boot.itb ../boot.txt ../mkscr  "${pkgdir}/boot/"
}

