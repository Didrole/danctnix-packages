# Maintainer: Danct12 <danct12@disroot.org>
pkgname=anbox-image-aarch64
pkgver=0_20200520
pkgrel=2
pkgdesc="Anbox image for ARM64"
arch=(aarch64)
url="https://anbox.postmarketos.org"
license=('custom')
depends=(anbox)
makedepends=(squashfs-tools)
provides=(anbox-image)
_fdroidver=1007051
_fdroid="org.fdroid.fdroid_$_fdroidver.apk"
_fdroidprivver=2110
_fdroidpriv="org.fdroid.fdroid.privileged_$_fdroidprivver.apk"
source=(android.img::http://anbox.postmarketos.org/android-7.1.2_r39-anbox_arm64-userdebug.img
        https://f-droid.org/repo/$_fdroid
        https://f-droid.org/repo/$_fdroidpriv
        network-fix.patch)

prepare() {
  mkdir -p ./squashfs
  # Decompressing the image
  unsquashfs -f -d ./squashfs android.img

  cd ./squashfs

  patch -p1 -N < ../network-fix.patch

  # remove su, it's broken and less apps will refuse to start
  rm -v system/xbin/su

  # Install FDroid
  mkdir system/app/FDroid
  mkdir system/priv-app/FDroid
  # install -Dm 0644 doesn't work as it copies the symlink, hence cp
  cp -L "$srcdir"/$_fdroid system/app/FDroid/$_fdroid
  cp -L "$srcdir"/$_fdroidpriv system/priv-app/FDroid/$_fdroidpriv
  chmod 0644 system/app/FDroid/$_fdroid
  chmod 0644 system/priv-app/FDroid/$_fdroidpriv
}

package() {
  mksquashfs "$srcdir"/squashfs "$srcdir"/android-fdroid.img -comp xz -no-xattrs
  install -Dm600 "$srcdir"/android-fdroid.img "$pkgdir"/var/lib/anbox/android.img
}

md5sums=('5c2e2a5fc1e6a388f6949b3df65b5c0c'
         'a5ba31782253a1a7c8f58faf1b75156d'
         'b8c42726bd6c8f3931e8a01890ce47b4'
         '1ef3b59fb14bbb7d17236af393d27474')
