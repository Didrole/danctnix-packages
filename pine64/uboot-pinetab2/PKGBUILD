# Maintainer: Danct12 <danct12@disroot.org>

buildarch=8

pkgname=uboot-pinetab2
pkgver=2023.07.02
pkgrel=1
pkgdesc="U-Boot for PineTab 2"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
depends=('uboot-tools')
makedepends=('bc' 'git' 'python' 'python-pyelftools' 'python-setuptools' 'swig')
replaces=(uboot-pinetab2-mainline)
install=${pkgname}.install
backup=("boot/boot.txt")

_commit_uboot="c294612d38e5434f549a7f591212249ca190edd3"
_commit_rkbin="d6ccfe401ca84a98ca3b85c12b9554a1a43a166c"

source=("$pkgname::git+https://github.com/Kwiboo/u-boot-rockchip.git#commit=$_commit_uboot"
        "rkbin::git+https://github.com/rockchip-linux/rkbin.git#commit=$_commit_rkbin"
        'config'
        'boot.txt'
        'mkscr'
        'update-u-boot')
md5sums=('SKIP'
         'SKIP'
         '4d819966a69fb0fc75bcfb5ca0f5a785'
         '635379a8a8063fe2ff147e54637fb2ed'
         '021623a04afd29ac3f368977140cfbfd'
         'ec7c2005cb5cc9a23d1eb0e1bf089bf9')

prepare() {
  cd ${pkgname}
  cp ${srcdir}/config .config
}

build() {
  cd ${pkgname}

  make EXTRAVERSION=-${pkgrel} \
    BL31=../rkbin/bin/rk35/rk3568_bl31_v1.42.elf \
    ROCKCHIP_TPL=../rkbin/bin/rk35/rk3566_ddr_1056MHz_v1.16.bin
}

package() {
  cd ${pkgname}

  mkdir -p "${pkgdir}/boot"

  cp ./idbloader.img "${pkgdir}/boot/idbloader.img"
  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
  mkdir -p "${pkgdir}"/usr/bin && cp ../update-u-boot "${pkgdir}"/usr/bin
}
