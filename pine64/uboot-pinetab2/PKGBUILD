# Maintainer: Danct12 <danct12@disroot.org>

buildarch=8

pkgname=uboot-pinetab2
pkgver=2017.09
pkgrel=1
pkgdesc="U-Boot for PineTab 2"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
provides=('uboot-tools') # our scripts wont work with older u-boot
conflicts=('uboot-tools')
makedepends=('git' 'bc' 'python' 'swig' 'dtc')
install=${pkgname}.install
backup=("boot/boot.txt")

_commit_uboot="77bd3405ed882d7ef632bad82cbc04608408f44f"
_commit_rkbin="1f2bd43aa12942849ec0f0e7ce26930865076bf3"

source=("$pkgname::git+https://github.com/Danct12/u-boot.git#commit=$_commit_uboot"
        "rkbin::git+https://github.com/armbian/rkbin.git#commit=$_commit_rkbin"
        'config'
        'boot.txt'
        'mkscr'
        'update-u-boot')
md5sums=('SKIP'
         'SKIP'
         '57a57038fac3054a0a05b1750867f7eb'
         '5b3133455f7e1087569b44a14bb6e249'
         '021623a04afd29ac3f368977140cfbfd'
         'ec7c2005cb5cc9a23d1eb0e1bf089bf9')

prepare() {
  cd ${pkgname}
  cp ${srcdir}/config .config
}

build() {
  cd ${pkgname}

  make EXTRAVERSION=-${pkgrel} \
    BL31=../rkbin/rk35/rk3568_bl31_v1.33.elf spl/u-boot-spl.bin u-boot.dtb u-boot.itb

  # uboot-tools
  make tools-all
}

package() {
  cd ${pkgname}

  mkdir -p "${pkgdir}/boot"

  ./tools/mkimage -n rk3568 -T rksd -d \
    ${srcdir}/rkbin/rk35/rk3566_ddr_1056MHz_v1.13.bin:spl/u-boot-spl.bin \
    idbloader.img

  cp ./idbloader.img "${pkgdir}/boot/idbloader.img"
  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
  mkdir -p "${pkgdir}"/usr/bin && cp ../update-u-boot "${pkgdir}"/usr/bin

  # uboot-tools
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -Dt "$pkgdir"/usr/share/man/man1/ doc/mkimage.1
}
