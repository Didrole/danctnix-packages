# Maintainer: Danct12 <danct12@disroot.org>
pkgbase=postmarketos-tweaks-app
pkgname=("$pkgbase" "$pkgbase-phosh")
pkgver=0.5.0
pkgrel=1
pkgdesc="Tweaks tool for phone UIs"
url="https://gitlab.com/postmarketOS/postmarketos-tweaks"
license=('GPL')
arch=('any')
depends=('libhandy' 'python' 'python-gobject' 'python-yaml' 'gtk3')
makedepends=('python-setuptools' 'meson')
source=("https://gitlab.com/postmarketOS/postmarketos-tweaks/-/archive/$pkgver/postmarketos-tweaks-$pkgver.tar.gz")

build() {
    arch-meson postmarketos-tweaks-$pkgver output
    meson compile -C output
}

package_postmarketos-tweaks-app() {
    DESTDIR="$pkgdir" meson install -C output
    mv "$pkgdir"/usr/share/postmarketos-tweaks/phosh.yml "$srcdir"/phosh.yml
}

package_postmarketos-tweaks-app-phosh() {
     mkdir -p "$pkgdir"/usr/share/postmarketos-tweaks
     mv "$srcdir"/phosh.yml "$pkgdir"/usr/share/postmarketos-tweaks/phosh.yml
}

md5sums=('9a80cf9147d7a13911ac78ff67f4a26c')
