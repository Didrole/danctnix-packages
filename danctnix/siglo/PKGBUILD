# Maintainer: Danct12 <danct12@disroot.org>
pkgname=siglo
pkgver=0.7.2
pkgrel=1
pkgdesc="GTK app to sync InfiniTime watch with PinePhone"
arch=(any)
url="https://github.com/alexr4535/siglo"
license=('MPL')
depends=(bluez bluez-utils python python-gobject dbus-python python-gatt python-requests python-pyxdg)
makedepends=(git meson)
_commit=0bea672bd23c310d92f9c6eb8615888b9f68945f # tags/v0.7.2
source=("git+https://github.com/alexr4535/siglo.git#commit=$_commit")

pkgver() {
  cd $pkgname
  git describe --tags | cut -c 2-
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

md5sums=('SKIP')
