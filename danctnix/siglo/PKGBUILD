# Maintainer: Danct12 <danct12@disroot.org>
pkgname=siglo
pkgver=0.8.8
pkgrel=1
pkgdesc="GTK app to sync InfiniTime watch with PinePhone"
arch=(any)
url="https://github.com/alexr4535/siglo"
license=('MPL')
depends=(bluez bluez-utils python python-gobject dbus-python python-gatt python-requests python-pyxdg)
makedepends=(git meson)
_commit=2f75a51b9c3f8ab052b2639a0b40d46a2867d1c0 # tags/v0.8.8
source=("git+https://github.com/alexr4535/siglo.git#commit=$_commit")

pkgver() {
  cd $pkgname
  git describe --tags | cut -c 2-
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

md5sums=('SKIP')
