# Maintainer: Danct12 <danct12@disroot.org>
pkgname=siglo
pkgver=0.9.4
pkgrel=1
pkgdesc="GTK app to sync InfiniTime watch with PinePhone"
arch=(any)
url="https://github.com/alexr4535/siglo"
license=('MPL')
depends=(bluez bluez-utils python python-gobject dbus-python python-gatt python-requests python-pyxdg)
makedepends=(git meson)
_commit=78f7b6c2aa553aaf088c6e1a67b29946bf96d32c # tags/v0.9.4
source=("git+https://github.com/alexr4535/siglo.git#commit=$_commit")

pkgver() {
  cd $pkgname
  git describe --tags | cut -c 2-
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

md5sums=('SKIP')
