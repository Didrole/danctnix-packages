# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

# DANCTNIX: - Force privacy screen inclusion to return 0 even the kernel does not have it

pkgname=mkinitcpio
pkgver=38.1
pkgrel=1.1
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
conflicts=(
  'systemd<255.4-2'
  'cryptsetup<2.7.0-3'
  'mdadm<4.3-2'
  'lvm2<2.03.23-3'
)
checkdepends=('bats' 'bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch')
install=mkinitcpio.install
sha512sums=('e727509badc528f45f2b193b3f49c202df41d4e75067bebd44c22ebc59f635d4a9596bc671d609d8941644f3a246267f7a199946730ba474040a1f24b94f663c'
            'SKIP'
            '52f9913b8c297919db34844439df0799d8699aa75f9d2adbdaf2bb3e68eee54ec9d27946ecd9e2053fef4057ae1859dedc7d99d8da7110b31ff9ac6970e970c1'
            '7c0ea267567c23c37dc5b5aac587e64885ff36ec9818a250687b40ad4903ea74d50b033d1b1a1dd46f968cab53f70f3da96af171d8f2fdf1aacaca328fbd4e2d'
            '674729df541f852153fb634b391df2b2af037efc9f2561c9ed1c211ce8b05e21f30f603a44c552283b449534b3f2ab95150fc0adc088069c86f91dfede5f7f3c')
b2sums=('625455bb1140688bcdf04c946eb6fa1da53deaa221b2c8090c173aef1d7fc617227aa0674344f3c18d5b9ab77a093725856f4f0cd3b8a33462a2ac742f0dbf11'
        'SKIP'
        '3c8ca4927c9eb39c8923e1000cf301605e2ded6d18d5d551941fdd8fc2c4a4b24ceff6593cf7afa02391f8559fafce5c80cfffd854612323905a41c90f55bb43'
        'c089ac622e6550c0ad9dc361c552e524b547f1124c127e8f32d7bd25ad45be7cbfe6afe1e0152d104f7c9c6a7e0d54f850b90b579bee33f9466a71c4cd40fa22'
        'dc5cd0fee2dc44cc76000e4875672b4e8b6cd53a09e7e98df2f648a8706dd276a06836bbe74903ad16d3633f104ac33a641d08e15a035cb379d92cb0fd0f84f3')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch

  # Since we do not have privacy screen modules enabled, there's no need for this
  # it also workaround an error when generating initramfs.
  sed -i '/add_checked_modules_from_symbol/ s/\([^ ].*drm_privacy_screen_register.*\)/# \1/' \
    install/kms
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
