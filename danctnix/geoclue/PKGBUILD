# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

# DANCTNIX: Danct12 <danct12@disroot.org>
#  - Add patches from Purism

pkgname=geoclue
pkgver=2.5.7
pkgrel=4
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
arch=(x86_64 armv7h aarch64)
url="https://www.freedesktop.org/wiki/Software/GeoClue/"
license=(LGPL)
depends=(libsoup json-glib libmm-glib avahi geocode-glib)
makedepends=(systemd gobject-introspection git vala meson gtk-doc libnotify)
optdepends=('libnotify: Demo Agent')
provides=("geoclue2=$pkgver-$pkgrel")
conflicts=(geoclue2)
replaces=(geoclue2)
backup=(etc/geoclue/geoclue.conf)
_commit=3bb60dd600a8b247fd6049e7e5b1c73c2c5fc0e8  # tags/2.5.7
source=("git+https://gitlab.freedesktop.org/geoclue/geoclue.git#commit=$_commit"
        0001-sources-Don-t-compute-movement-for-low-accuracy-sour.patch
        0005-Bump-mm-glib-dep-to-1.10.patch
        0006-Use-tracking-area-code-TAC-as-the-LAC-when-in-LTE-mo.patch
        client-info-Support-cgroup-v2.patch
        geoclue.conf-Add-phosh.patch)
sha256sums=('SKIP'
            '22ac3c979cf2c95ba4f0d323cef2e10946667763c9bcec125a00cb213e502818'
            '78adb17f88e87ed3c3a2fc925f8b65c260cba53551a206370d1639d1129fb46b'
            'b20f01c6527b4aeb2844287754e7f78111a057296bd11d13950fc9ea1e580713'
            '2d7c5bb485b29942dd9475b14ec3d2a43a0a873f774a5c4f1b50f7a1bcd80ff5'
            '4800d0669a6a7b456df4f8fb91e8dffa2184e58c2e7054955760f4833fc9f398')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # config patches (configurable hardcoded API key)
  git cherry-pick -n 95c9ad4dc176 1a00809a0d89

  # https://sources.debian.org/patches/geoclue-2.0/2.5.7-2/
  git cherry-pick -n f9347ca7d51b c3eba034eb03 12445fb134ee

  # https://sources.debian.org/patches/geoclue-2.0/2.5.7-2/fix-nowifi-query.patch/
  git revert -n 194529c7e712 715cfbf5bec8

  git apply -3 ../0001-sources-Don-t-compute-movement-for-low-accuracy-sour.patch

  # Purism patches
  patch -p1 -N < ../0005-Bump-mm-glib-dep-to-1.10.patch
  patch -p1 -N < ../0006-Use-tracking-area-code-TAC-as-the-LAC-when-in-LTE-mo.patch
  patch -p1 -N < ../client-info-Support-cgroup-v2.patch
  patch -p1 -N < ../geoclue.conf-Add-phosh.patch
}

build() {
  arch-meson $pkgname build \
    -D dbus-srv-user=geoclue \
    -D dbus-sys-dir=/usr/share/dbus-1/system.d
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  echo 'u geoclue - "Geoinformation service" /var/lib/geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  echo 'd /var/lib/geoclue 0755 geoclue geoclue' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set sw=2 et:
