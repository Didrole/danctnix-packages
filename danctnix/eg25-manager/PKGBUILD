# Maintainer: Danct12 <danct12@disroot.org>
# Contributor : Philip MÃ¼ller <philm@manjaro.org>

pkgname=eg25-manager
pkgver=0.3.0+6+gf2593b6
pkgrel=1
_commit=f2593b62b10513f94039d5c9ba995905b23e7eb4
pkgdesc="Daemon for managing the Quectel EG25 modem"
arch=('aarch64')
url="https://gitlab.com/mobian1/devices/eg25-manager"
license=('GPL3')
depends=('libgudev' 'libgpiod' 'libusb' 'libmm-glib')
makedepends=('meson' 'ninja' 'git')
optdepends=('modemmanager: for Phosh'
            'ofono: for Lomiri or Plasma Mobile')
replaces=('danctnix-eg25-misc')
source=(git+$url.git#commit=${_commit}
        eg25-manager.service)
sha256sums=('SKIP'
            '93cf52f71752865978b7cdd8e8f425fc4e10d1267aede03f90325994db200010')

pkgver() {
  cd ${pkgname}
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson ${pkgname} build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 $srcdir/eg25-manager.service $pkgdir/usr/lib/systemd/system/eg25-manager.service
}
