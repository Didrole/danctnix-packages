# U-Boot: PinePhone/PineTab
# Maintainer: Danct12 <danct12@disroot.org>

buildarch=8

pkgname=uboot-pinephone
pkgver=v2020.01_megous
pkgrel=1
pkgdesc="U-Boot for Pine64"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('bc' 'git' 'python' 'swig' 'dtc')
install=${pkgname}.install
source=('git+https://megous.com/git/u-boot#commit=0ab6225154c3d8b74f06fb3b181b52a9a64b4602'
        'git+https://megous.com/git/atf#commit=16ddf2b7d0b4bc0367b179e3c6f3f3e60947068a'
        '0001-sunxi-Add-Pinetab-support.patch'
        '0001-ATF-set-fno-stack-protector.patch'
        'boot.txt'
        'mkscr')
md5sums=('SKIP'
         'SKIP'
         'ef3c87f019a75c74212fa83d31e6a0cd'
         'b07140c0c7d20f63fc28722e509e349b'
         '06d85506cdc2054ba76b552bf29499c2'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd atf
  git apply ../0001-ATF-set-fno-stack-protector.patch

  cd ../u-boot
  git apply ../0001-sunxi-Add-Pinetab-support.patch
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd atf

  make PLAT=sun50i_a64 DEBUG=1 bl31
  cp build/sun50i_a64/debug/bl31.bin ../u-boot

  cd ../u-boot

  # This builds the PineTab.
  make distclean
  make pinetab_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinetab.bin

  # This builds the PinePhone.
  make pinephone_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinephone.bin
}

package() {
  cd u-boot

  mkdir -p "${pkgdir}"/boot

  cp u-boot-sunxi-with-spl-pinetab.bin "${pkgdir}"/boot
  cp u-boot-sunxi-with-spl-pinephone.bin "${pkgdir}"/boot

  cp ../boot.txt ../mkscr "${pkgdir}"/boot
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d "${pkgdir}"/boot/boot.txt "${pkgdir}"/boot/boot.scr
}
