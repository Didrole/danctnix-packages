# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.45.0
pkgrel=1
pkgdesc="Phosh-specific settings app"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
    'gtk4'
    'gsound'
    'libadwaita'
    'libgmobile'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'glib2-devel'
    'meson'
    'python-packaging'
    'wayland-protocols'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
b2sums=('635fa29f376421674d3bb5ff8990bcae8460c32374f1ae684d3ec31bd0d2a7a2c2acd47587ad3b1f0eed9e53a2b1208f77a10a2e8e61e9ccf374a26e2c2a3089'
        'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    arch-meson $pkgname-$pkgver output
    meson compile -C output
}

package() {
    meson install -C output --skip-subprojects --destdir "$pkgdir"
}
