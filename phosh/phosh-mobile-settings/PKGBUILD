# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.35.1
pkgrel=1
pkgdesc="A settings app for mobile specific things (mostly for Phosh)"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings"
license=('GPL')
depends=(
    'gtk4'
    'gsound'
    'libadwaita'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'meson'
    'ninja'
    'wayland-protocols'
)
_commit="0207281e1718b25fe94f3f345993dd5f9684bb1c"  # tags/v0.35.1
source=("phosh-mobile-settings::git+https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings.git#commit=${_commit}")

pkgver() {
    cd phosh-mobile-settings
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson phosh-mobile-settings output --wrap-mode default
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}

md5sums=('SKIP')
