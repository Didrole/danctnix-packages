# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.37.0
pkgrel=2
pkgdesc="A settings app for mobile specific things (mostly for Phosh)"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings"
license=('GPL-3.0-or-later')
depends=(
    'gtk4'
    'gsound'
    'libadwaita'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'meson'
    'ninja'
    'wayland-protocols'
)
_commit="dce617a9afe10e14843439a672bcc672ba1bb3b0"  # tags/v0.37.0
source=("phosh-mobile-settings::git+https://gitlab.gnome.org/World/Phosh/phosh-mobile-settings.git#commit=${_commit}")

pkgver() {
    cd phosh-mobile-settings
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson phosh-mobile-settings output --wrap-mode default
    meson compile -C output
}

package() {
    meson install -C output --skip-subprojects --destdir "$pkgdir"
}

md5sums=('SKIP')
