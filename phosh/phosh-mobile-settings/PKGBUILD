# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-mobile-settings
pkgver=0.42.0
pkgrel=1
pkgdesc="A (Phosh-specific) settings app for mobile"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://phosh.mobi"
license=('GPL-3.0-or-later')
depends=(
    'gtk4'
    'gsound'
    'libadwaita'
    'libgmobile'
    'lm_sensors'
    'phosh'
    'wayland'
)
makedepends=(
    'git'
    'glib2-devel'
    'meson'
    'python-packaging'
    'wayland-protocols'
)
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('9659db7c04e8c7836d669c8b1e37f3fcf50d3547f37534e6e3d028ed5356ef26'
            'SKIP')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

build() {
    arch-meson $pkgname-$pkgver output
    meson compile -C output
}

package() {
    meson install -C output --skip-subprojects --destdir "$pkgdir"
}
