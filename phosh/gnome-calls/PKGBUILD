# Maintainer: Danct12 <danct12@disroot.org>
pkgname=gnome-calls
pkgver=46.3
pkgrel=1
pkgdesc="Make and receive PSTN phone calls"
url="https://gitlab.gnome.org/GNOME/calls"
license=('GPL-3.0-or-later')
arch=('x86_64' 'armv7h' 'aarch64')
depends=(
  'callaudiod'
  'evolution-data-server'
  'feedbackd'
  'folks'
  'gobject-introspection'
  'gom'
  'gst-plugins-bad'
  'gst-plugins-good'
  'gtk3'
  'libhandy'
  'libpeas'
  'modemmanager'
  'sofia-sip'
  'wayland-protocols'
  )
makedepends=(
  'git'
  'glib2-devel'
  'meson'
  'ninja'
  'python-docutils'
  'python-packaging'
  'vala'
  )
checkdepends=('xorg-server-xvfb')
replaces=('calls')
_commit="f8c247148717bbad9414a9b246d88d1e685e68ed" # tags/v46.3
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit")
sha256sums=('4eddd4cbdd10c9f3f611573b05b5ce7275f1dab3f6ae1aeda9e4b2fc2ea2494b')

pkgver() {
  cd calls
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd calls
  git submodule--helper update --init

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson calls output
  ninja -C output
}

## SIP test hanging
#check() {
#  xvfb-run ninja -C output test
#}

package() {
  DESTDIR="$pkgdir" ninja -C output install
}
