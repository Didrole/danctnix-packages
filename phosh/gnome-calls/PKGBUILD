# Maintainer: Danct12 <danct12@disroot.org>
pkgname=gnome-calls
pkgver=43.3
pkgrel=1
pkgdesc="Make and receive PSTN phone calls"
url="https://gitlab.gnome.org/GNOME/calls"
license=('GPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('evolution-data-server' 'feedbackd' 'folks' 'gobject-introspection'
         'gom' 'gtk3' 'libhandy' 'libpeas' 'modemmanager' 'callaudiod' 'sofia-sip'
         'wayland-protocols')
makedepends=('meson' 'ninja' 'vala' 'git' 'python-docutils')
replaces=('calls')
_commit="e6d83159c40225f0af3b767b3c78e1710b555d33" # tags/v43.3
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit")

pkgver() {
    cd calls
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd calls
  git submodule--helper update --init

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson calls output
  ninja -C output
}

package() {
  DESTDIR="$pkgdir" ninja -C output install
}

md5sums=('SKIP')
