# Maintainer: Danct12 <danct12@disroot.org>
pkgname=calls
pkgver=0.3.4
pkgrel=2
pkgdesc="Make and receive PSTN phone calls"
url="https://gitlab.gnome.org/GNOME/calls"
license=('GPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('evolution-data-server' 'feedbackd' 'folks' 'gobject-introspection'
         'gom' 'gtk3' 'libhandy' 'libpeas' 'modemmanager' 'callaudiod' 'sofia-sip'
         'wayland-protocols')
makedepends=('meson' 'ninja' 'vala' 'git')
_commit="4948148bfeb0730197c5f3faf7d507a248fcf579" # tags/0.3.4
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit"
        367.patch)

prepare() {
  cd calls

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson calls output
  ninja -C output
}

package() {
  DESTDIR="$pkgdir" ninja -C output install
}

md5sums=('SKIP'
         'a8a152f16645e9299ebc44d80f0b445d')
