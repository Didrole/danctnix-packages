# Maintainer: Danct12 <danct12@disroot.org>
pkgname=chatty
pkgver=0.6.0
pkgrel=1
pkgdesc="Messaging client"
url="https://source.puri.sm/Librem5/chatty"
license=('GPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('evolution-data-server' 'feedbackd' 'gtk4' 'libhandy' 'libolm' 'libpurple'
    'gspell' 'libmm-glib' 'mmsd-tng' 'gnome-desktop')
makedepends=('meson' 'ninja' 'git' 'itstool')
_commit="da26b33a98137e542efd840eadae426b0cdb7b42"  # tags/v0.6.0
source=(git+https://source.puri.sm/Librem5/chatty.git#commit=${_commit})

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd $pkgname
    git submodule update --init
}

build() {
    arch-meson $pkgname output
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
md5sums=('SKIP')
