# Maintainer: Danct12 <danct12@disroot.org>
pkgname=gnome-console
pkgver=43.0
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop."
url="https://gitlab.gnome.org/GNOME/console"
license=('GPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('vte4' 'gtk4' 'glib2' 'libadwaita' 'libgtop' 'sassc')
makedepends=('git' 'meson' 'libnautilus-extension')
replaces=('kgx')
_commit="cffddb1a7bbf8ad1b1616cc026909411e218db1a"  # tags/43.0
source=("${pkgname}::git+https://gitlab.gnome.org/GNOME/console.git#commit=$_commit")

pkgver() {
    cd ${pkgname}
    git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
    arch-meson ${pkgname} output -Dtests=true
    ninja -C output
}

check() {
    ninja test -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
md5sums=('SKIP')
