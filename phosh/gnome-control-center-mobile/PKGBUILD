# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=gnome-control-center
pkgname=gnome-control-center-mobile
pkgver=3.38.6
pkgrel=1
pkgdesc="GNOME's main interface to configure various aspects of the desktop - Purism fork"
url="https://source.puri.sm/Librem5/gnome-control-center"
license=(GPL2)
arch=(x86_64 armv7h aarch64)
depends=(accountsservice cups-pk-helper gnome-bluetooth gnome-desktop
         gnome-online-accounts gnome-settings-daemon gsettings-desktop-schemas gtk3
         libgtop nm-connection-editor sound-theme-freedesktop upower libpwquality
         gnome-color-manager smbclient libmm-glib libgnomekbd grilo libibus
         libgudev bolt udisks2 libhandy gsound colord-gtk modemmanager)
makedepends=(docbook-xsl git python meson)
checkdepends=(python-dbusmock python-gobject xorg-server-xvfb)
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: WebDAV file sharing'
            'gnome-remote-desktop: screen sharing'
            'rygel: media sharing'
            'openssh: remote login')
provides=(gnome-control-center)
conflicts=(gnome-control-center)
_commit=54eb734eaaa95807dd805fbe4e4ad0dceb787736
source=("git+https://gitlab.gnome.org/GNOME/gnome-control-center.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        # purism
        0001-Add-helper-for-new-connection-editor.patch
        0001-Add-new-connection-editor.patch
        0002-wifi-Use-new-connection-editor.patch
        0020-user-panel-Hide-automatic-login-switch.patch
        0002-UserAccount-fix-window-resizing-issue.patch
        wwan-Add-new-panel-for-modem-management.patch
        Expose-touchpad-settings-if-synaptics-is-in-use.patch
        Revert-build-Bump-build-dependency-on-polkit.patch
        display-panel-Adjust-to-Librem-5-display-size.patch
        location-panel-Allow-label-to-wrap.patch
        0001-avatar-chooser-Adapt-to-work-on-librem5.patch
        0002-datetime-Fix-timezone-selection-map.patch
        0003-display-Request-a-smaller-size-for-arrangment-widget.patch
        0004-online-accounts-Make-edit-account-dialog-resizable.patch
        0005-power-show-brightness-slider-in-a-separate-row.patch
        0006-sound-fix-test-dialog-on-small-screen.patch
        0007-UserAccount-Allow-stack-to-have-different-size.patch
        Add-patches-to-check-if-phone.patch
        display-Let-button-box-have-vertical-orientation-on-.patch
        notification-Hide-lockscreen-notification-option-on-.patch
        region-Remove-preview-button-from-input-row-on-phone.patch
        shell-Hide-some-panels-on-phones.patch
        user-accounts-Constrain-passwords-to-digits-on-phone.patch
        user-panel-hide-add-user-unlock-buttons.patch
        # mobian
        0002-search-ellipsize-button-label-to-fit-small-screens.patch
        0003-usage-ellipsize-labels-to-fit-small-screens.patch
        0004-power-add-more-suspend-timing-options.patch
        0005-applications-ellipsize-title-label-to-always-fit-sma.patch)
sha256sums=('SKIP'
            'SKIP'
            'b07cb99b3629a3722184f698c7cb1e53596f700db1c630b2f6658c9cf7e3bd12'
            '505f5c3fdaa6165d848cccb74f7ce061f11ba41248ef29ce6be0aa5d0f81055d'
            'ebc8c1081cd1130d5b6e68105f6a0a35733145394c0bf6f57664d385d858c288'
            '303c6cb3cd03c49b30b63e17f1156d4a0ee04ed0a3b45b989b2a9cae30eb0a4d'
            'e6d1c422c713bedfa21a9e6b502b3c996fc8f2b8b48c394f1e2bc3789a15bd0f'
            'bdc213da23f27d11da8a2510da913c887bd19a269fa085e3a4bc481ed8765689'
            'd90f11cb8b0758ec3742619fe0388953a87c7f8b7e8658c29125ad2915d149be'
            '5407ade296a710e04812e75529ad662cfa6ab05862e509ec255d0957d75ac569'
            'ee3627291349bfa01a062608a157fc0db41923bdab30ec63525bc5722c6c1f34'
            '2b7f13002bc2ab6c0d8437f56fa45c35311163b648690182b27a9a5bb6ad6d9d'
            '6d5119a6c27d3abaa9a87f54826917ed56fd3d498efd94a0793e5a797ba3d743'
            '3a696422320435cbaddd3dae238714e035645e41e838cd73d28a690e695d2980'
            '6cefc65f7d1f3f50bf26db0f6e88f40c0b4506cfc1a1b0e0878e3ccd1084d527'
            'a059d013968461fe9dfdb1cc4dd5f9bd3833beb0532b49a74c2ea05818f45c84'
            '9e8137cb904ea04adb534575d535b375175c6ed50b6602ab46baacb401d8c105'
            '2942d4867480430ef06ec7353823977bc3d4fa247639fd1228da2a0037d3c011'
            '98e840678f3308757bde02c0f65bd3806fd2ff21de14e3bf5cc347b139e7ce06'
            '438b595e4ae963263f4349be4a9080a5d02edf7b345e821133f3f6de0a8c3b84'
            'c7c5e40ce8b1a05693ba0322cef07c05efc3bbd8edafd771e7cd70b26a21656e'
            '46d8585bb00906cd7ac8e02d47fee505d699cdab0e2bb076b2c3202f31eab12b'
            'b49ba7c81ed8e27be7789f7ecab786797c14bb8a1b35853ee23c7d77caf23b6d'
            'ac1c218bc582f1533e66e799a4a8a77e818fa7b4430718c07d54151c311fff08'
            '0866afc5acd31e20a47a604b8d6f458fd94afc62ba1b20f71857719e6822fc59'
            'cbd0b44fc8881e07c3be169100f728a758a41f85aa5b10c94bc711dc74fc61ff'
            '6ecf0e6c3dcba4e3f96e3a1231b79076d481836684ecc22c1dd707838ccde322'
            '58c7c22b6ef68c96006cd5e0ca50b89f16274fc2110888fcc7f3fefc580f73f6'
            '7a9afc5a28919894194f33eb8067ed6dae85247aa2a8ad9457a7341c9dde5033'
            'd8376df953b02250720899ec5dcb151449ef4cfb745019432b8cf4cd7d65305f')

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed 's/^GNOME_CONTROL_CENTER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd ${_pkgname}
  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git submodule update

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson ${_pkgname} build -Dcheese=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  echo "X-Purism-FormFactor=Workstation;Mobile;" >> "$pkgdir/usr/share/applications/gnome-control-center.desktop"
}
