# Maintainer: Danct12 <danct12@disroot.org>
pkgbase=phosh-wallpapers
pkgname=('phosh-wallpapers' 'plymouth-theme-phosh' 'sound-theme-phosh')
pkgver=0.41.0
pkgrel=1
pkgdesc="Wallpapers for Phosh UI"
arch=(any)
url="https://gitlab.gnome.org/guidog/phosh-wallpapers"
makedepends=('meson' 'plymouth')
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha256sums=('4d8853be325f9bb6df2a15dd4222e35fbcf70629330b9d2b4fc6b295101766a6')

build() {
    arch-meson $pkgname-v$pkgver output
    meson compile -C output
}

package_phosh-wallpapers() {
    pkgdesc="Wallpapers for Phosh UI"
    license=('CC-BY-SA-4.0 AND GPL-3.0-or-later')
    meson install -C output --destdir "$pkgdir"
    mv "$pkgdir"/usr/share/plymouth "$srcdir"/usr_share_plymouth
    mv "$pkgdir"/usr/share/sounds "$srcdir"/usr_share_sounds
}

package_plymouth-theme-phosh() {
    pkgdesc="Plymouth theme for Phosh UI"
    license=('CC-BY-SA-4.0 AND GPL-3.0-or-later')
    depends=('plymouth')
    mkdir -p "$pkgdir"/usr/share
    mv "$srcdir"/usr_share_plymouth "$pkgdir"/usr/share/plymouth
}

package_sound-theme-phosh() {
    pkgdesc="Sound theme for Phosh UI"
    license=('CC0 AND CC-BY-SA-4.0')
    mkdir -p "$pkgdir"/usr/share
    mv "$srcdir"/usr_share_sounds "$pkgdir"/usr/share/sounds
}
