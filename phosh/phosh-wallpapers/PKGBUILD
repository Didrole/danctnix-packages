# Maintainer: Danct12 <danct12@disroot.org>
pkgbase=phosh-wallpapers
pkgname=('phosh-wallpapers' 'plymouth-theme-phosh')
pkgver=0.37.0
pkgrel=1
pkgdesc="Wallpapers for Phosh UI"
arch=(any)
url="https://gitlab.gnome.org/guidog/phosh-wallpapers"
makedepends=('meson' 'plymouth')
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha256sums=('451bcbbf6e7359966a0c49bde8dee83e2b78e56267d1fc43e909392ff77b2775')

build() {
    arch-meson $pkgname-v$pkgver output
    meson compile -C output
}

package_phosh-wallpapers() {
    pkgdesc="Wallpapers for Phosh UI"
    license=('CC-BY-SA-4.0 AND GPL-3.0-or-later')
    meson install -C output --destdir "$pkgdir"
    mv "$pkgdir"/usr/share/plymouth "$srcdir"/usr_share_plymouth
}

package_plymouth-theme-phosh() {
    pkgdesc="Plymouth theme for Phosh UI"
    license=('CC-BY-SA-4.0 AND GPL-3.0-or-later')
    depends=('plymouth')
    mkdir -p "$pkgdir"/usr/share
    mv "$srcdir"/usr_share_plymouth "$pkgdir"/usr/share/plymouth
}
