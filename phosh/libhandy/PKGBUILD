# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

# DanctNIX: - Added patches to fix dragging from buttons
#           - Add !623 (https://gitlab.gnome.org/GNOME/libhandy/-/merge_requests/623)

pkgname=libhandy
pkgver=1.0.0
pkgrel=10
pkgdesc="Library full of GTK+ widgets for mobile phones"
url="https://gitlab.gnome.org/GNOME/libhandy"
arch=(x86_64 armv7h aarch64)
license=(LGPL)
depends=(glib2 gtk3)
makedepends=(git gobject-introspection meson vala glade)
provides=(libhandy-1.so)
_commit=94313c206258860b2428712e7ece1d02c5177857  # tags/1.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/libhandy.git#commit=$_commit"
        swipe-tracker-Special-case-dragging-from-buttons.patch
        623.patch)
sha256sums=('SKIP'
            'c4ef96e3fa76ac4a6fcd4008bffce9b59c9e8ca01b1588cda97915cdab99bee1'
            '94627c515498be0003877c0890399074eb54eba07d9ccc9fdbcc0d9664b7fbd1')

prepare() {
  cd libhandy

  # Fix build with glade 3.38.0
  git cherry-pick -n 65ff089b7f346f3809e9e0050ea32ac003465ede

  patch -p1 -N < ../swipe-tracker-Special-case-dragging-from-buttons.patch
  patch -p1 -N < ../623.patch
}

pkgver() {
  cd libhandy
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson libhandy build \
    -D gtk_doc=true \
    -D examples=false
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
