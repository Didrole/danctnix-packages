# Maintainer: Danct12 <danct12@disroot.org>
pkgname=livi
pkgver=0.2.0
pkgrel=1
pkgdesc="GTK4 video player using GStreamer as backend"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/guidog/livi"
license=('GPL-3.0-or-later')
depends=(
    'gstreamer'
    'gst-plugins-bad'
    'gst-plugins-base'
    'gtk4'
    'libadwaita'
)
makedepends=(
    'git'
    'glib2-devel'
    'meson'
    'python-packaging'
)
checkdepends=('xorg-server-xvfb')
_commit="cdba7c74909b8575f20740728a917ff5356b2007" # tags/v0.2.0
source=("livi::git+https://gitlab.gnome.org/guidog/livi.git#commit=${_commit}")

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd livi
    git submodule update --init
}

build() {
    arch-meson livi output
    meson compile -C output
}

check() {
    xvfb-run meson test -C output
}

package() {
    meson install -C output --destdir "$pkgdir"
}

md5sums=('00d517c3149f55e74c8cc5d7bb3642df')
