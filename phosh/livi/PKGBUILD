# Maintainer: Danct12 <danct12@disroot.org>
pkgname=livi
pkgver=0.3.1
pkgrel=1
pkgdesc="GTK4 video player using GStreamer as backend"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/guidog/livi"
license=('GPL-3.0-or-later')
depends=(
    'gstreamer'
    'gst-plugins-bad'
    'gst-plugins-base'
    'gtk4'
    'libadwaita'
)
makedepends=(
    'glib2-devel'
    'meson'
    'python-packaging'
)
checkdepends=('xorg-server-xvfb')
source=("https://gitlab.gnome.org/guidog/livi/-/archive/v${pkgver}/livi-v${pkgver}.tar.gz")

build() {
    arch-meson ${pkgname}-v${pkgver} output
    meson compile -C output
}

check() {
    xvfb-run meson test -C output
}

package() {
    meson install -C output --destdir "$pkgdir"
}

md5sums=('6f6a29500a0c1acd753c8f3162a11233')
