# Maintainer: Danct12 <danct12@disroot.org>
pkgname=squeekboard
pkgver=1.38.0
pkgrel=2
pkgdesc="An on-screen-keyboard input method for Wayland"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/squeekboard"
license=('GPL-3.0-or-later')
depends=('feedbackd' 'gtk3' 'gnome-desktop' 'libbsd' 'wayland-protocols')
makedepends=('rust' 'meson' 'ninja' 'python-packaging')
source=(https://gitlab.gnome.org/World/Phosh/squeekboard/-/archive/v$pkgver/squeekboard-v$pkgver.tar.gz)

build() {
    arch-meson ${pkgname}-v${pkgver} output
    ninja -C output
}

check() {
    meson test -C output --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}

md5sums=('aa9ffc53a53a94c4a3af7883644a0b9b')
