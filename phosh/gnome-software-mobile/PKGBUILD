# Contribotor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>
# Maintainer: Danct12 <danct12@disroot.org>

pkgname=gnome-software-mobile
pkgver=3.38.2
pkgrel=1
pkgdesc="GNOME Software Tools (w/ Purism patches)"
url="https://gitlab.gnome.org/GNOME/gnome-software"
arch=(x86_64 armv7h aarch64)
license=(GPL2)
makedepends=(appstream-glib gnome-desktop libpackagekit-glib flatpak fwupd ostree
             docbook-xsl git gobject-introspection gspell gtk-doc meson valgrind
             gnome-online-accounts libxmlb libhandy)
_commit=fcafd7ed2c8c3a8ca8062f409aac304ceb30def2
source=(https://gitlab.gnome.org/GNOME/gnome-software/-/archive/$_commit/gnome-software-$_commit.tar.bz2
        0001-meson-Add-dependency-on-libhandy-1.0.0.patch
        0002-shell-Make-the-search-bar-adaptive.patch
        0003-shell-Give-a-title-and-an-icon-to-the-main-stack-pag.patch
        0004-updates-page-Notify-the-stack-when-it-needs-attentio.patch
        0005-loading-page-Make-it-adaptive.patch
        0006-shell-Set-a-minimum-size.patch
        0007-details-page-Make-it-better-fit-in-smaller-narrower-.patch
        0008-librem5-details-page-Port-to-phones.patch
        0009-librem5-Drop-the-custom-.needs-attention-style-class.patch
        0010-librem5-Replace-the-custom-stack-switcher-by-a-view-.patch
        0011-librem5-updates-page-Drop-the-Checking-label-from-th.patch
        0012-librem5-updates-section-Make-the-header-vertical.patch
        0013-librem5-app-row-Make-vertical.patch
        0014-librem5-app-row-Port-to-phones.patch
        0015-librem5-review-row-Port-it-to-phones.patch
        0016-librem5-category-page-Port-to-phones.patch
        0017-librem5-shell-Make-the-notification-box-vetical.patch
        0018-librem5-upgrade-banner-Make-it-fit-narrow-screens.patch
        0019-librem5-updates-page-Make-it-fit-narrow-screens.patch
        0020-librem5-prefs-dialog-Make-it-adaptive.patch
        0021-librem5-details-page-Drop-the-screenshots.patch
        0022-librem5-update-dialog-Don-t-show-a-double-back-butto.patch
        0023-librem5-update-dialog-Reduce-the-default-width.patch
        0024-Revert-Launch-software-properties-gtk-in-place-of-the-sources-dialog.patch
        0025-category-page-fix-ui-template.patch)
sha256sums=('bdfab7cdfd153e4ffeeab623a67a9add28140738985ebb0a96a7f2a34f04d92a'
            '305ab16c92a777789617ed4641a40c908b18491f74c6f16de4843c9a4f0cc027'
            '7d1e568e3cc96e97444868937f8ef926c80f8208d3f8c639a655d7045fb5cf03'
            'd784f4ee5223148ee292f82fe880815566edd4418ae3f8b61e48f7b1f8316d8b'
            '7033ba4fdb7b52cebda127f5fbae49ea2e21e3255aca049712c9a52e627cbc06'
            'bb250c7d131f358d7e90707934cb8087b0dfb66085f4e57d195f91ad1612b3f9'
            'd0661262e708fec068763b839f1b3cc12eaa5581c09838559bdba8f2736516aa'
            '42d2fff2ec34698821e79f114f09f6baa51116639187e6c3909c1e343986b76e'
            'd6452f420e6a2d07b4158daeef7194e55146fe746e6660da5580713628414475'
            'b98ab493aca36708ba7b47f09ff551729da0d927e9d249be040274ad89f784d7'
            'a89be87654904f32375814e59eb2d6fbef684f183fcc1f684b3c13e56d8ad370'
            '8d99a0d043ba17861ed7bcfdbf5d50021f002c707240302bbb68f7cc8c5c429f'
            '24e9b356eebd5fbb4098540c1e98757eaa965145e061d0144356e0e4359333a2'
            '352ab1df137fa047c5a3869878bbec7b9a1e6d8295020f806406e17a43da4cc8'
            '54a921de34b3112f7c00a7023486749bc2b7baef1aef48719928bf591ecda5ab'
            'c3f6055fcd0adfa06b44c867d234b10f88f1e2a320cde2425b681e3f7083f730'
            '1a8d7e25d743bdb51772b4bd690ac83e1f8c04063e135e8d0214ee9ade2cdd22'
            '03afa4e61dfd4e18cb86777935106d2d4fb0912b7e373e31d54b3f60bec7918b'
            'f0bc28c6609bebd58695901d5e5ab146d156a79ba8cc6ff5210171f1ae2ee101'
            'd962eb06d3b2a6cb084966113cd9ac7e5c189deb2a60fa183e4c1d831154e1a1'
            '54b9bf686d64ca91c066a4a6beb618ad37fcb029110da14435ca30660ed2da4f'
            '9aebc597d614389cc2e93532cfdf63b7bbda25d51a133a9726767e3cfa64e6d5'
            '62089b348ce24ecda70e7db710668ea7319d7d70940449ebc24901104aa51e6c'
            'ef18e6b913caafee92bd619c9340f869cd121c8c4c34415a6c423fe5e0d1ff74'
            'bfd10ec468ec3a6c15e0872dab099a6e99b525155470cffdfd03506a90e8f6a4'
            '147d5e5f164cba96e861b2e87e6a0f55c10ca4d1e1ce8e3fddfad1f9d935073d')

prepare() {
  cd gnome-software-$_commit

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson gnome-software-$_commit build -Dmalcontent=false -Dsysprof=disabled
  ninja -C build
}

check() {
  # build container troubles
  meson test -C build --print-errorlogs || :
}

package() {
  provides=(gnome-software gnome-software-packagekit-plugin)
  conflicts=(gnome-software gnome-software-packagekit-plugin)
  depends=(libxmlb gnome-desktop gsettings-desktop-schemas gspell libpackagekit-glib
           gnome-online-accounts appstream-glib archlinux-appstream-data packagekit)
  optdepends=('flatpak: Flatpak support plugin'
              'fwupd: fwupd support plugin'
              'ostree: OSTree support plugin')

  DESTDIR="$pkgdir" meson install -C build
}
