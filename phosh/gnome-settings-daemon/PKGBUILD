# DANCTNIX: - Apply !285 and !327 as phosh release wanted.

# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-settings-daemon
pkgver=44.1
pkgrel=2
pkgdesc="GNOME Settings Daemon"
url="https://gitlab.gnome.org/GNOME/gnome-settings-daemon"
arch=(x86_64 aarch64)
license=(GPL)
depends=(
  dconf
  gcr-4
  geoclue
  geocode-glib-2
  gnome-desktop
  gsettings-desktop-schemas
  gtk3
  libcanberra-pulse
  libgudev
  libgweather-4
  libnm
  libnotify
  librsvg
  libwacom
  nss
  pulseaudio
  pulseaudio-alsa
  systemd
  upower
)
makedepends=(
  docbook-xsl
  git
  libxslt
  meson
  python
  usbguard
)
checkdepends=(
  python-dbusmock
  python-gobject
)
optdepends=('usbguard: USB protection support')
groups=(gnome)
backup=(etc/xdg/Xwayland-session.d/00-xrdb)
_commit=7cc9a78f1d562ea759d1776ed061d4339a6f4610  # tags/44.1^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-settings-daemon.git#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
  '285.patch'
)
b2sums=('SKIP'
        'SKIP'
        'fe2a8069d9c5a6db70de039cefe8097b09a7326b08169f22bba78d2bfbe405c66c7701765c7b6cb88c7296e0c8c6fe983f13e491fa4482f7059e8d07df407dd2')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git apply ../285.patch

  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
