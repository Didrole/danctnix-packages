# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.4.3
pkgrel=1
pkgdesc="A compositor for phones based on wlroots"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://source.puri.sm/Librem5/phoc"
license=('GPL')
depends=('gobject-introspection' 'gnome-desktop' 'libinput' 'mutter'
         'xcb-util-errors' 'xcb-util-wm')
makedepends=('meson' 'ninja')

# We don't want to maintain the wlroots package, so we static compile it
_commit_wlr="7723a09a226d812aa51d1439c2b5e8e21238d45c"
source=(https://source.puri.sm/Librem5/phoc/-/archive/v$pkgver/phoc-v$pkgver.tar.gz
        https://source.puri.sm/Librem5/wlroots/-/archive/$_commit_wlr/wlroots-$_commit_wlr.tar.gz
        0001-seat-Don-t-notify-on-key-release.patch
        0002-seat-inhibit-touch-events-when-in-power-save-mode-or.patch
        cde70286d67a8dc5a4a900e3c71cdcae068faafc.patch)

prepare() {
	rm -r ${pkgname}-v${pkgver}/subprojects/wlroots
	mv wlroots-$_commit_wlr ${pkgname}-v${pkgver}/subprojects/wlroots

	# Apply patches from Mobian
	cd ${pkgname}-v${pkgver}
	patch -p1 -N < ../0001-seat-Don-t-notify-on-key-release.patch
	patch -p1 -N < ../0002-seat-inhibit-touch-events-when-in-power-save-mode-or.patch

	# Upstream patches
	patch -p1 -N < ../cde70286d67a8dc5a4a900e3c71cdcae068faafc.patch
}

build() {
	arch-meson ${pkgname}-v${pkgver} output -Dtests=false
	ninja -C output
}

package() {
	DESTDIR="$pkgdir" ninja -C output install

	# Install scale-to-fit helper
	install -Dm755 ${pkgname}-v${pkgver}/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

	# Remove unnecessary files
	rm -r "$pkgdir"/usr/lib
	rm -r "$pkgdir"/usr/include
}

md5sums=('f4d5a18f72b72d5c3b2a3ff57cd96a2c'
         'f643daf07d8cc9b5642381206de026c6'
         '9779076dbffbf2eab2b25aa7bc1edff4'
         '6beda6022c1402852e2ea8afb9a8fbeb'
         'c1561e42dd537dc3b99c6a97ad453266')
