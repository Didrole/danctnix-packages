# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar-mobile
_pkgname=gnome-calendar
pkgver=44.1
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64 armv7h aarch64)
license=(GPL)
depends=(
  evolution-data-server
  geoclue
  geocode-glib-2
  gsettings-desktop-schemas
  libadwaita
  libedataserverui4
  libgweather-4
  libsoup3
)
makedepends=(
  git
  meson
)
optdepends=('gnome-control-center: Manage online accounts')
provides=('gnome-calendar')
conflicts=('gnome-calendar')
_commit=0d0ef588ad192094c63b73d768a8a01d091d61a7  # tags/44.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#commit=$_commit"
        '0001-gcal-window-make-it-fit-phone-screens.patch')
sha256sums=('SKIP'
            '77490904cebe070e9ca5d102d86f51fcbcc5d2d5041e62e22d8400d1676627b6')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
