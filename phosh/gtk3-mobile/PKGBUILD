# Contributor Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Danct12 <danct12@disroot.org>

pkgname=gtk3-mobile
pkgver=3.24.24
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (Built with Purism patches)"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://www.gtk.org/"
depends=(atk cairo libxcursor libxinerama libxrandr libxi libepoxy gdk-pixbuf2 dconf
         libxcomposite libxdamage pango shared-mime-info at-spi2-atk wayland libxkbcommon
         adwaita-icon-theme json-glib librsvg wayland-protocols desktop-file-utils mesa
         cantarell-fonts colord rest libcups libcanberra fribidi)
makedepends=(gobject-introspection gtk-doc git glib2-docs sassc)
license=(LGPL)
install=gtk3.install
_commit=de8329b3ce4b235eac9ddcbb6cd121cc83dd9e74  # tags/3.24.24^0
source=("https://gitlab.gnome.org/GNOME/gtk/-/archive/$_commit/gtk-$_commit.tar.bz2"
        settings.ini
        gtk-query-immodules-3.0.hook
        aboutdialog-Port-to-phones.patch
        Add-GtkHdyClamp.patch
        Add-GtkHdyLeaflet.patch
        Add-GtkHdyNavigationDirection.patch
        Add-GtkHdyShadowHelper.patch
        Add-GtkHdySqueezer.patch
        Add-GtkHdyStackableBox.patch
        Add-GtkHdySwipeable-and-GtkHdySwipeTracker.patch
        Add-GtkHdyViewSwitcherBar.patch
        Add-GtkHdyViewSwitcherButton.patch
        Add-GtkHdyViewSwitcher.patch
        Add-GtkHdyViewSwitcherTitle.patch
        Add-org.gtk.Settings.Purism.patch
        Add-padding-for-HdyViewSwitcherTitle.patch
        Add-the-view-sidebar-symbolic-icon.patch
        demos-examples-tests-Don-t-distribute-built-files.patch
        dialog-Maximize-resizable-dialogs-on-phones.patch
        events-Compress-touch-update-events.patch
        gdk-Don-t-distribute-generated-files-in-tarballs.patch
        gtkprivate-Add-an-API-to-check-if-phone.patch
        gtk-Really-don-t-distribute-built-files.patch
        headerbar-Use-a-back-button-in-dialogs-on-mobile.patch
        infobar-Move-the-action-area-below.patch
        librem5-Drop-reftests.patch
        messagedialog-Set-orientation-based-on-device.patch
        Port-file-chooser-to-phones.patch
        Reduce-the-font-chooser-minimum-size.patch
        scrolledwindow-Set-deceleration-value-based-on-the-device.patch
        theme-Add-libhandy-styles.patch
        window-Don-t-allow-window-dragging-on-phones.patch
        window-Maximize-resizable-pseudo-dialogs-on-mobile.patch)
sha256sums=('c3cfdc9871eccf84ada47b73f060450eae9183dda1e6e296311a49a997cec23e'
            '01fc1d81dc82c4a052ac6e25bf9a04e7647267cc3017bc91f9ce3e63e5eb9202'
            'de46e5514ff39a7a65e01e485e874775ab1c0ad20b8e94ada43f4a6af1370845'
            'f5a46311e324a21ba84378f7afcf84a6b63bf30863f87819082abaf69e07ffea'
            '055702d2855d6f595551abf4dfd2f33166ce58a115aa6d82d95c87cbf759ad9f'
            '738b36e213fdab145fa78df7d4c31b8d6f5ec62d5cf4fec846b970643950c4ac'
            'f252da6b99f004ee7264d68e7a401badcdd1bbeea6f7407b60e4cfc6d08ffb44'
            'cff8e0b66daf93a6d5b4d417b33e5e3f64932581ac2bfad6321a43b29e6756d7'
            '91128e1f58d40bb3f82c5244a6a3a38b3aec2f32a735e0ec5bbb29345ec73dcf'
            '33626ab49e7d214f014e70ac597ca3327878cf1c1fece7a4681a9a36a4403b24'
            'a739aff24e3fe58c1e794a471056743f4b0ed8848b936e5ddd9078f0ebb890dc'
            '0ab599df456629ae8769cc1f81e727264745ce5301ccf17fa4914c23a3d9f39b'
            '669f3dec1fcee38ca7861b98f8e23a7b7478e0675bc10efeacc6b89f26b821b7'
            '91eb025a859983815dc5587a6bb394fcb83c43c0bb765710383fd56200da5844'
            '1717d0e5fa4329672b81c586cdd462baf9e1b0aba78d3d4d27b414ee129b350e'
            'ffa4226a88ae1c9681fcf38724cad003716e6030509e0df8d5fb8f8ae5e8e82b'
            'd4a44b1a7bb646b2232841372ab2f7f73b29b24f61bf8cbc6fd03d314d5bf55d'
            '6311690f6a151ae310ad895909f1bb116fd341e7a0e3cc601a100f3e134a0847'
            '68db37056d39785da655f680decbd669bd67a4f0b3861c2687a7c59e5a514ff9'
            '2a19167711aa4acf675150401072686e3d5e2045c3611ded748d33a3495bdbbc'
            '7dc4f66d115dec74bf5affae180e5de5e299b47410b2532473b3fb8a34aee709'
            '842f5221aeeae82d0d86356dc4566bc6d57060819b57957da1c178fcab61b7c8'
            'f21ea0945e68ad79fa833cc548e274c6dfa63ddbc5adb9fdbf0eec956a7ba435'
            'bce0a688ba8efb4986d5e4cd756371eeaf0c40a756d9bf116db6a907336cf505'
            'd54bddf36c652f75760ef35939d01300f3e8fda3bcc889a0d56a7be9bd2e9802'
            '63b123a8f598a8405630a74edf8d41c31d6aad7120f8db42252ba9c1127599d9'
            'eb23fd4fb6fdf355b3d9c8502772eb5bafc5a6c498de37b994d26354153d0c36'
            '91d304378bd57e8b581b7982acca77f3385c2dad0ac0bdb31435b0315d32e3ae'
            '88e7e4cab427440efcf099c1b97858a2e81385f4e2f37e758385c7a9061ba1ea'
            '2e6b997431443c53449af401bc5d4a3645c55ba3268809cf5c28cdfbe98e379c'
            '7c524fd9dc1d954bac76a0ec0f0b17b7e370f22e6243eaf1592b5c12b91fb060'
            '2095abcc72a8776e12dc56e8627b0e953339664d5781c423abd6e12eed78fe1c'
            '680a7de058d00f49ddac543392151502e9daca8f72a5ffd00543c4e93a9b6311'
            '4298dbfb786d26162f3012b86975669a3538903eb8fc103c354385ca64dd0c15')

prepare() {
  cd gtk-$_commit

  # apply phone patches
  msg2 "Applying Purism GTK patches"
  patch -p1 -N < ../gdk-Don-t-distribute-generated-files-in-tarballs.patch
  patch -p1 -N < ../gtk-Really-don-t-distribute-built-files.patch
  patch -p1 -N < ../demos-examples-tests-Don-t-distribute-built-files.patch
  patch -p1 -N < ../librem5-Drop-reftests.patch
  patch -p1 -N < ../Add-GtkHdyViewSwitcherButton.patch
  patch -p1 -N < ../Add-GtkHdyViewSwitcher.patch
  patch -p1 -N < ../Add-GtkHdyViewSwitcherBar.patch
  patch -p1 -N < ../Add-GtkHdySqueezer.patch
  patch -p1 -N < ../Add-GtkHdyViewSwitcherTitle.patch
  patch -p1 -N < ../Add-GtkHdyShadowHelper.patch
  patch -p1 -N < ../Add-GtkHdyNavigationDirection.patch
  patch -p1 -N < ../Add-GtkHdySwipeable-and-GtkHdySwipeTracker.patch
  patch -p1 -N < ../Add-GtkHdyStackableBox.patch
  patch -p1 -N < ../Add-GtkHdyLeaflet.patch
  patch -p1 -N < ../Add-GtkHdyClamp.patch
  patch -p1 -N < ../theme-Add-libhandy-styles.patch
  patch -p1 -N < ../Add-padding-for-HdyViewSwitcherTitle.patch
  patch -p1 -N < ../Add-org.gtk.Settings.Purism.patch
  patch -p1 -N < ../gtkprivate-Add-an-API-to-check-if-phone.patch
  patch -p1 -N < ../aboutdialog-Port-to-phones.patch
  patch -p1 -N < ../Add-the-view-sidebar-symbolic-icon.patch
  patch -p1 -N < ../Port-file-chooser-to-phones.patch
  patch -p1 -N < ../messagedialog-Set-orientation-based-on-device.patch
  patch -p1 -N < ../scrolledwindow-Set-deceleration-value-based-on-the-device.patch
  patch -p1 -N < ../dialog-Maximize-resizable-dialogs-on-phones.patch
  patch -p1 -N < ../window-Maximize-resizable-pseudo-dialogs-on-mobile.patch
  patch -p1 -N < ../headerbar-Use-a-back-button-in-dialogs-on-mobile.patch
  patch -p1 -N < ../infobar-Move-the-action-area-below.patch
  patch -p1 -N < ../Reduce-the-font-chooser-minimum-size.patch
  patch -p1 -N < ../window-Don-t-allow-window-dragging-on-phones.patch
  patch -p1 -N < ../events-Compress-touch-update-events.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtk-$_commit

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-schemas-compile \
    --enable-x11-backend \
    --enable-broadway-backend \
    --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  depends+=(gtk-update-icon-cache)
  provides=(gtk3 gtk3-print-backends libgtk-3.so libgdk-3.so libgailutil-3.so)
  conflicts=(gtk3 gtk3-print-backends)

  DESTDIR="$pkgdir" make -C gtk-$_commit install

  install -Dt "$pkgdir/usr/share/gtk-3.0" -m644 settings.ini
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-query-immodules-3.0.hook

  rm "$pkgdir/usr/bin/gtk-update-icon-cache"
  rm "$pkgdir/usr/share/man/man1/gtk-update-icon-cache.1"
}

# vim:set ts=2 sw=2 et:
