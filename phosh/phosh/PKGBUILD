# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.4.1
pkgrel=2
pkgdesc="The Librem5 Shell"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://source.puri.sm/Librem5/phosh"
license=('GPL')
depends=('gtk3' 'libhandy' 'gnome-desktop' 'gnome-session' 'upower' 'libpulse'
         'gcr' 'feedbackd' 'libnm' 'phoc')
makedepends=('meson' 'ninja')
_commit_gvc="ae1a34aafce7026b8c0f65a43c9192d756fe1057"
source=("https://source.puri.sm/Librem5/phosh/-/archive/v$pkgver/phosh-v$pkgver.tar.gz"
	"https://gitlab.gnome.org/GNOME/libgnome-volume-control/-/archive/$_commit_gvc/libgnome-volume-control-$_commit_gvc.tar.gz"
	'pam_phosh'
	'sm.puri.OSK0.desktop'
	'0001-panel-implement-reboot-action.patch')

prepare() {
	cd ${pkgname}-v${pkgver}
	rm -r subprojects/gvc
	mv "$srcdir/libgnome-volume-control-$_commit_gvc" subprojects/gvc

	# Apply patches
	patch -p1 -N < ../0001-panel-implement-reboot-action.patch
}

build() {
	arch-meson ${pkgname}-v${pkgver} output
	ninja -C output
}

package() {
	DESTDIR="$pkgdir" ninja -C output install

	install -Dm644 "$srcdir"/$pkgname-v$pkgver/debian/phosh.service \
		"$pkgdir"/usr/lib/systemd/system/phosh.service
	install -Dm644 "$srcdir"/pam_phosh \
		"$pkgdir"/etc/pam.d/phosh
	install -Dm644 "$srcdir"/sm.puri.OSK0.desktop \
		"$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('a178488f58c8b146b440f8f9eb03283c'
         'c08f04485db11997525c139346faecc8'
         '6d5a0d561f8362bf91f85c236a88395d'
         '30c7febb5cbbac40984fb50d66a88639'
         'bf102ac7588c4d4ba4096053cd90a9e6')
