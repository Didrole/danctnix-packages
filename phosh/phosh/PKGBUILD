# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.8.1
pkgrel=1
pkgdesc="The Librem5 Shell"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://source.puri.sm/Librem5/phosh"
license=('GPL')
depends=('gtk3' 'libhandy' 'gnome-desktop' 'gnome-session' 'upower' 'libpulse'
         'gcr' 'feedbackd' 'libnm' 'phoc')
makedepends=('meson' 'ninja')
_commit_gvc="ae1a34aafce7026b8c0f65a43c9192d756fe1057"
source=("https://source.puri.sm/Librem5/phosh/-/archive/v$pkgver/phosh-v$pkgver.tar.gz"
	"https://gitlab.gnome.org/GNOME/libgnome-volume-control/-/archive/$_commit_gvc/libgnome-volume-control-$_commit_gvc.tar.gz"
	'0001-Revert-home-Bind-key-to-show-overview.patch'
	'434.patch'
	'pam_phosh'
	'sm.puri.OSK0.desktop'
	'phosh.service')

prepare() {
	cd ${pkgname}-v${pkgver}

	# gnome-shell dependency removal
	patch -p1 -N < ../0001-Revert-home-Bind-key-to-show-overview.patch

	# auto rotation
	patch -p1 -N < ../434.patch

	rm -r subprojects/gvc
	mv "$srcdir/libgnome-volume-control-$_commit_gvc" subprojects/gvc
}

build() {
	arch-meson ${pkgname}-v${pkgver} output
	ninja -C output
}

package() {
	DESTDIR="$pkgdir" ninja -C output install

	install -Dm644 "$srcdir"/phosh.service \
		"$pkgdir"/usr/lib/systemd/system/phosh.service
	install -Dm644 "$srcdir"/pam_phosh \
		"$pkgdir"/etc/pam.d/phosh
	install -Dm644 "$srcdir"/sm.puri.OSK0.desktop \
		"$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('e2168db446210c76fb522251d372eb55'
         'c08f04485db11997525c139346faecc8'
         '610811849fcbe27a9648d7a5a1b9c2ed'
         '0960255886287b1154f85224e87d9058'
         '6d5a0d561f8362bf91f85c236a88395d'
         '30c7febb5cbbac40984fb50d66a88639'
         'efeeab2f55315f576c88e45c494edc7c')
