# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=evince
pkgname=$_pkgname-mobile
pkgver=43.1
pkgrel=1
pkgdesc="Document viewer (PDF, PostScript, XPS, djvu, dvi, tiff, cbr, cbz, cb7, cbt)"
url="https://wiki.gnome.org/Apps/Evince"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL')
_commit=47461d6dfbf2bb5d183f8db5dc43d205b68241e6  # tags/43.1^0
source=("git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit"
        '0001-toolbar-Allow-to-toggle-the-sidebar-button.patch'
        '0002-toolbar-Allow-hiding-zoom-annotation-and-page-widgets.patch'
        '0003-properties-view-Reduce-the-label-width-chars.patch'
        '0004-window-Port-it-to-phones.patch')
sha256sums=('SKIP'
            '1838583a3e91e402309ad57f94c04e8792e8716feb83bcef01715c56b5d50aee'
            '4a3426cd9d5887644008d98df1f5cae266853d211711e119ca6017c7c10f1869'
            'bb31c4e7dc302d66df70c5d6e3dc4cbfa0ccb5c11d669db0015dec21991d93a6'
            '199c20375a81dba6bbadb5ef512527f575792d101aed5dbbb68206f2c5f5f538')
provides=(libev{document,view}3.so $_pkgname $_pkgname-lib-docs)
conflicts=($_pkgname)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre dconf libsecret libsynctex
         gsettings-desktop-schemas gnome-desktop libarchive gst-plugins-base-libs gspell libhandy
         gvfs)
makedepends=(texlive-bin gobject-introspection gi-docgen git meson appstream-glib yelp-tools)

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build -D ps=enabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
