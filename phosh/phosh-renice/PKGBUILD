# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh-renice
pkgver=0.0.0_20200812
pkgrel=0
pkgdesc="A quick hack to prioritise calls, NetworkManager, phoc, phosh, and squeekboard on the PinePhone."
arch=('any')
url="https://github.com/KeironO/phosh-renice"
license=('MIT')
depends=('phosh' 'util-linux')
_commit="34f6ea2a88af604b43d236df050637d2f60358b2"
source=("https://github.com/KeironO/phosh-renice/archive/$_commit.tar.gz")

prepare() {
	cd ${pkgname}-$_commit
	sed -i "s/\/usr\/local/\/usr/g" phosh-renice.service
}

package() {
	install -Dm755 "$srcdir"/${pkgname}-$_commit/phosh_renice.sh \
		"$pkgdir"/usr/bin/phosh_renice
	install -Dm644 "$srcdir"/${pkgname}-$_commit/phosh-renice.service \
		"$pkgdir"/usr/lib/systemd/system/phosh-renice.service

	mkdir -p "$pkgdir"/usr/lib/systemd/system/multi-user.target.wants
	ln -sfv ../phosh-renice.service \
		"$pkgdir"/usr/lib/systemd/system/multi-user.target.wants/phosh-renice.service
}

md5sums=('c767e234cc1c9e8f1dacd11b5b1c0aa8')
