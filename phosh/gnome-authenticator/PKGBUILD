# Maintainer: Danct12 <danct12@disroot.org>
 
pkgname="gnome-authenticator"
pkgver=3.32.2
pkgrel=0
pkgdesc="2FA code generator for GNOME"
arch=('any')
url="https://gitlab.gnome.org/World/Authenticator"
license=("GPL3")
depends=('pyzbar' 'python-pyotp' 'python-pillow' 'python-beautifulsoup4' 'libhandy' 'python-pyfavicon' 'python-yoyo-migrations')
makedepends=('meson' 'ninja' 'gobject-introspection' 'git')
source=("https://gitlab.gnome.org/World/Authenticator/-/archive/$pkgver/Authenticator-$pkgver.tar.gz"
        '0001-Removed-all-references-to-self.init_template.patch')
md5sums=('5ca5f6ee8abb0ded676cee94179ae42d'
         '172d06439e7ec3f538a59343a1307989')

prepare() {
    cd Authenticator-$pkgver
    patch -p1 -N < ../0001-Removed-all-references-to-self.init_template.patch
}

build() {
    arch-meson Authenticator-$pkgver build
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
}
