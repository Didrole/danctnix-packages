# Maintainer: Aren <aren@peacevolution.org>
# Contributor: dni <office@dnilabs.com>

pkgname=('sxmo-utils' 'sxmo-utils-sway' 'sxmo-utils-dwm')
pkgver=1.13.0
pkgrel=1
pkgdesc="Utility scripts, programs, and configs that hold the sxmo UI environment together"
url="https://git.sr.ht/~mil/sxmo-utils"
arch=('x86_64' 'armv7h' 'aarch64')
license=('AGPL3')
makedepends=('libx11' 'xorgproto' 'linux-headers' 'busybox')
install=sxmo-utils.install
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~mil/sxmo-utils/archive/$pkgver.tar.gz"
        '0001-Systemd-compatibility.patch'
        '00-sxmo.rules'
        '10-sxmo.conf'
        'sxmo-setpermissions.service')
sha512sums=('a41d7ee2b71cee003aa7afb5d6f49253868d3e87794617b1831df43600a4807c519c3d82396b106058b0ce644fd4dd97492407398a7e2ded83e9b2bc59333254'
            '74505731cc51a6d14d4e4ca081ec83cc0e190ee64ae2adf5b16983c255a5aca7b6e17fc040d5d7705695cdcf92ff0d8cd3764ebc71dfbd5e67a9b2055aec13fc'
            'd705fc9611716a73c89da94bf3abc9292e9069bc994b0bd097159f4babd83d4693d5cc3e42336739ac46a8c6b4a0f39d077047202901dc5066df75e597450fa7'
            'b69a09f98f8a5cad8164cd48c5f6a78f2ce0e9818fb77ccf2a3e243cd0f2023e438022b20aad9e6b5edd90e1bb862326db79dc423630682d4c43e897090d3da1'
            '532c3045f52f872704876bdf56afbc7470c5d17e46655dd4040d545b6d3eb34a8791909e554c560471f9494c7382107839bcff1e48be242d5eea0092482165cd')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 < '../0001-Systemd-compatibility.patch'
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package_sxmo-utils() {
  backup=('etc/doas.conf')
  depends=(
    # Shell utilities
    'bc'
    'curl'
    'gawk'
    'gojq'
    'less'
    'net-tools' # netstat command
    'pnc'

    # Core dependcies
    'callaudiod'
    'opendoas'
    'geoclue'
    'iio-utils'
    'inotify-tools'
    'mmsd-tng'
    'mnc' # for suspend
    'modemmanager'
    'ncurses' # for the weather script
    'networkmanager'
    'superd'
    'vvmd'
    'xdg-user-dirs'

    # Audio
    'alsa-utils'
    'bluez'
    'bluez-utils'
    'libpulse'
    'pamixer'
    'pulseaudio'
    'pulseaudio-alsa'

    # Core GUI dependencies
    'sxmo-ui'
    'dunst'
    'libnotify'
    'light'
    'lisgd'
    'mpv'
    'nerd-fonts-fira-mono' # system font
    'nerd-fonts-terminus')
  optdepends=('bemenu-ncurses: sxmo menus over ssh'
              'bonsai: better multikey script'
              'clickclack: haptic feedback'
              'codemadness-frontends: Youtube & Reddit scripts'
              'mediainfo: sound recorder'
              'pipewire-pulse: recommended sound server'
              'sfeed: Rss and atom feeds'
              'vim: The default editor'
              'yt-dlp: Play videos from the web')

  make -C "$pkgname-$pkgver" OPENRC=0 DESTDIR="$pkgdir" install-scripts

  # Allow access to ModemManager and NetworkManager
  install -Dm644 -t "$pkgdir/usr/share/polkit-1/rules.d" "00-sxmo.rules"

  # Prevent logind from handling the power button
  install -Dm644 -t "$pkgdir/etc/systemd/logind.conf.d" "10-sxmo.conf"

  # Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # fix directory permissions, TODO this should be done upstream
  chmod 750 "$pkgdir/etc/doas.d"

  # HACK: doas is built without --with-doas-confdir so install the sxmo config directly
  install -Dm640 "$pkgdir/etc/doas.d/sxmo.conf" "$pkgdir/etc/doas.conf"

  install -Dm644 "$srcdir/sxmo-setpermissions.service" "$pkgdir/usr/lib/systemd/system/sxmo-setpermissions.service"
}

package_sxmo-utils-sway() {
  pkgdesc="Packages required to use sway as a ui for sxmo"
  url="https://sr.ht/~mil/Sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  replaces=('sxmo-ui-sway-meta')
  install=
  depends=('bemenu-wayland'
           'foot'
           'grim'
           'mako'
           'slurp'
           'sway'
           'swaybg'
           'swayidle'
           'wayout'
           'wl-clipboard'
           'wob'
           'wtype'
           'wvkbd'
           'xorg-xwayland')
  optdepends=('sxmo-sway: better touch event handling')

  make -C "$pkgbase-$pkgver" DESTDIR="$pkgdir" install-sway
}

package_sxmo-utils-dwm() {
  pkgdesc="Packages required to use dwm as a ui for sxmo"
  url="https://sr.ht/~mil/Sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  replaces=('sxmo-ui-dwm-meta')
  install=
  depends=('autocutsel'
           'conky'
           'feh'
           'ttf-dejavu' # for conky
           'otf-latin-modern' # for conky
           'terminus-font' # dmenu & xcalc
           'svkbd'
           'sxiv'
           'sxmo-dmenu'
           'sxmo-dwm'
           'sxmo-st'
           'unclutter'
           'xcalib'
           'xclip'
           'xdotool'
           'xsel'
           'xorg-xdpyinfo'
           'xorg-xinput'
           'xorg-xprop'
           'xorg-xrandr'
           'xorg-xrdb'
           'xorg-xset'
           'xorg-xsetroot'
           'xorg-xwininfo'
           'xprintidle')

  make -C "$pkgbase-$pkgver" DESTDIR="$pkgdir" install-dwm
}
