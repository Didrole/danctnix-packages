# Maintainer: Aren <aren@peacevolution.org>
# Contibutor: dni <office@dnilabs.com>

pkgname=('sxmo-utils' 'sxmo-ui-sway-meta' 'sxmo-ui-dwm-meta')
pkgver=1.11.1
pkgrel=1
pkgdesc="Utility scripts, programs, and configs that hold the Sxmo UI environment together"
url="https://git.sr.ht/~mil/sxmo-utils"
arch=('x86_64' 'armv7h' 'aarch64')
license=('AGPL3')
makedepends=('libx11' 'xorgproto' 'linux-headers' 'busybox')
install=sxmo-utils.install
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~mil/sxmo-utils/archive/$pkgver.tar.gz"
        '0001-Systemd-compatibility.patch'
        '0001-sxmo_hook_statusbar.sh-remove-to-fix-pango-markup.patch'
        '00-sxmo.rules'
        'sxmo-setpermissions.service')
backup=('etc/doas.conf')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 < '../0001-Systemd-compatibility.patch'
  patch -p1 < '../0001-sxmo_hook_statusbar.sh-remove-to-fix-pango-markup.patch'
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package_sxmo-utils() {
  depends=(
    # Core cli dependencies
    'busybox'
    'callaudiod'
    'curl'
    'doas'
    'ffmpeg'
    'gawk'
    'geoclue'
    'gojq'
    'iio-utils'
    'inotify-tools'
    'mmsd-tng'
    'mnc' # for suspend
    'modemmanager'
    'ncurses' # for the weather script
    'networkmanager'
    'pn'
    'superd'
    'v4l-utils'
    'vvmd'
    'xdg-user-dirs'

    # Audio
    'alsa-utils'
    'bluez'
    'bluez-utils'
    'libpulse'
    'pamixer'
    'pulseaudio'
    'pulseaudio-alsa'

    # Core GUI dependencies
    'sxmo-ui'
    'dunst'
    'libnotify'
    'light'
    'lisgd'
    'bemenu'
    'mediainfo'
    'mpv'
    'nerd-fonts-fira-mono' # system font
    'nerd-fonts-terminus')
  optdepends=('bonsai: better multikey script' # TODO: upload a pkgbuild to the aur
              'clickclack: haptic feedback'
              'codemadness-frontends: Youtube & Reddit scripts'
              'pipewire-pulse: recommended sound server'
              'sfeed: Rss and atom feeds'
              'vim: The default editor'
              'yt-dlp: Play videos from the web')

  make -C "$pkgname-$pkgver" OPENRC=0 DESTDIR="$pkgdir" install-scripts

  install -Dm644 -t "$pkgdir/usr/share/polkit-1/rules.d" "00-sxmo.rules"

  # Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # fix directory permissions, TODO this should be done upstream
  chmod 750 "$pkgdir/etc/doas.d"

  # Fix sxmo internal version, it wasn't properly bumped upstream
  echo "$pkgver" > "$pkgdir/usr/share/sxmo/version"

  # HACK: doas is built without --with-doas-confdir so install the sxmo config directly
  install -Dm640 "$pkgdir/etc/doas.d/sxmo.conf" "$pkgdir/etc/doas.conf"

  install -Dm644 "$srcdir/sxmo-setpermissions.service" "$pkgdir/usr/lib/systemd/system/sxmo-setpermissions.service"
}

package_sxmo-ui-sway-meta() {
  pkgdesc="Packages required to use sway as a ui for sxmo"
  url="https://sr.ht/~mil/Sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  install=
  depends=('bemenu-wayland'
           'foot'
           'grim'
           'mako'
           'seatd'
           'slurp'
           'sway'
           'swaybg'
           'swayidle'
           'wayout'
           'wl-clipboard'
           'wob'
           'wtype'
           'wvkbd'
           'xorg-xwayland')
  optdepends=('sxmo-sway: better touch event handling')

  make -C "$pkgbase-$pkgver" DESTDIR="$pkgdir" install-sway
}

package_sxmo-ui-dwm-meta() {
  pkgdesc="Packages required to use dwm as a ui for sxmo"
  url="https://sr.ht/~mil/Sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  install=
  depends=('autocutsel'
           'conky'
           'feh'
           'ttf-dejavu' # for conky
           'otf-latin-modern' # for conky
           'terminus-font' # dmenu & xcalc
           'svkbd'
           'sxiv'
           'sxmo-dmenu'
           'sxmo-dwm'
           'sxmo-st'
           'unclutter'
           'xcalib'
           'xclip'
           'xdotool'
           'xsel'
           'xorg-xdpyinfo'
           'xorg-xinput'
           'xorg-xprop'
           'xorg-xrandr'
           'xorg-xrdb'
           'xorg-xset'
           'xorg-xsetroot'
           'xorg-xwininfo'
           'xprintidle')

  make -C "$pkgbase-$pkgver" DESTDIR="$pkgdir" install-dwm
}

sha512sums=('fcc59168b37d565fca5fd033bfa8663ae014b3b2adfd6cb5d33a9d8b9583477291d5fa466cdbab9ea2d45df29f60313b2032bb5d18fdf1c450d64c00b5b2db42'
            'cd68928c83c1f1bed1fd8572a0c06ef111122ffbc969cf697b94067830106cef44183bf8ddd1296e51ef42ed60526a411979078bbb67b8b2a60db719f0c767cd'
            'bbdd90ceabb558c871485205bb824f379dc4bc2a70e3fad5ca2d08d5a8b9ee03933818e65c8354c50f46362a1646d8e8d15b3cc2b8be8fb54b10581673cc0c4f'
            'd705fc9611716a73c89da94bf3abc9292e9069bc994b0bd097159f4babd83d4693d5cc3e42336739ac46a8c6b4a0f39d077047202901dc5066df75e597450fa7'
            '532c3045f52f872704876bdf56afbc7470c5d17e46655dd4040d545b6d3eb34a8791909e554c560471f9494c7382107839bcff1e48be242d5eea0092482165cd')
